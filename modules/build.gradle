import org.gradle.plugins.ide.idea.model.IdeaLanguageLevel
import org.gradle.util.VersionNumber

plugins {
	id "de.set.ecj" version "1.4.1" apply false
	id "idea"
	
	// For BinTray publishing, do not apply but it is used by sub-projects
	id "com.jfrog.bintray" version "1.8.5" apply false
}

group = "cc.squirreljme.modules"
description = "Modules which are a part of SquirrelJME."

// Helper function to normalize the Maven version
static VersionNumber normalizeMavenVersion(Object __inVersion) {
	VersionNumber inVersion = VersionNumber.parse(__inVersion.toString())
	
	// If this is an even version, then this is a release version
	if ((inVersion.minor % 2) == 0)
		return inVersion
	
	// Otherwise odd numbers are considered to be "snapshot" versions
	return VersionNumber.parse(inVersion.toString() + "-SNAPSHOT")
}

// Every sub-project needs SquirrelJME specific pieces
subprojects {
	//apply plugin: "de.set.ecj"
	apply plugin: "java"
	apply plugin: "cc.squirreljme.plugin"
	apply plugin: "idea"
	apply plugin: "maven-publish"
	apply plugin: "com.jfrog.bintray"
	
	idea {
		module
		{
			// This is 1.8 because 1.7 is not really available
			jdkName = "1.8"
			
			// However, everything targets 1.7 otherwise
			languageLevel = new IdeaLanguageLevel("1.7")
			targetBytecodeVersion = JavaVersion.VERSION_1_7
		}
	}
	
	// Configure the compiler
	java {
		// Java ME 8 is effectively Java 7
        compileJava.sourceCompatibility = JavaVersion.VERSION_1_7
		compileJava.targetCompatibility = JavaVersion.VERSION_1_7

		// All files are always in UTF-8 format
		compileJava.options.encoding = "utf-8"

		// Emit deprecation errors
		compileJava.options.deprecation = true

		// Copy to tests
		compileTestJava.sourceCompatibility = compileJava.sourceCompatibility
		compileTestJava.targetCompatibility = compileJava.targetCompatibility
		compileTestJava.options.encoding = compileJava.options.encoding
		compileTestJava.options.verbose = compileJava.options.verbose
		compileTestJava.options.deprecation = compileJava.options.deprecation

		// Copy to JavaDoc
		javadoc.options.source = "1.7"
		javadoc.options.tags = [ "squirreljme.property",
			"squirreljme.env",
			"squirreljme.error",
			"squirreljme.syscallparam",
			"squirreljme.syscallreturn",
			"squirreljme.tsiparam",
			"squirreljme.configtype" ]
			
		// Build sources and JavaDoc as well
		withSourcesJar()
		withJavadocJar()
	}

	// Enable SquirrelJME Tests
	apply from: project.rootProject.findProject(":emulators:emulator-base").
		projectDir.toPath().resolve("enable-testing.gradle").toFile()
	
	// Publishing
	publishing {
		// What is being published?
		publications {
			maven(MavenPublication) {
				// Adjustments to the Maven POM
				pom {
					name = "SquirrelJME Module " + project.name
					url = "https://squirreljme.cc/"
					
					// SquirrelJME started to exist in 2016!
					inceptionYear = "2016"
					
					licenses {
						license {
							name = "GNU General Public License v3"
							url = "http://127.0.0.1:8080/file?name=LICENSE&ci=trunk"
						}
					}
					
					developers {
						developer {
							id = "stephanie.gawroriski"
							name = "Stephanie Gawroriski"
							email = "xerthesquirrel@gmail.com"
						}
					}
						
					scm {
						url = "https://squirreljme.cc/"
					}
					
					organization {
						name = "SquirrelJME"
						url = "https://squirreljme.cc/"
					}
					
					issueManagement {
						name = "GitHub"
						url = "https://github.com/SquirrelJME/SquirrelJME/issues"
					}
				}
				
				// Normal information
				groupId = "cc.squirreljme.modules"
				artifactId = project.name
				version = normalizeMavenVersion(rootProject.version)
				
				// What is being published?
				from components.java
			}
		}
		
		// Where is this to be published?
		repositories {
			// Publishing to local Maven
			mavenLocal()
		}
	}
	
	// Publishing to BinTray
	bintray {
		user = System.getProperty("bintray.user",
			Objects.toString(System.getenv("BINTRAY_USER"), "user_unset!"))
		key = System.getProperty("bintray.key",
			Objects.toString(System.getenv("BINTRAY_KEY"), "key_unset!"))
		
		publications = ['maven']
		
		pkg {
			repo = "SquirrelJME"
			name = "module-" + project.name
			licenses = ['GPL-3.0']
			vcsUrl = "https://squirreljme.cc/"
			
			version {
				name = normalizeMavenVersion(rootProject.version)
			}
		}
	}
}

// Properties for every sub-project, except for cldc-compact as it is special
configure(subprojects
	- project(":modules:cldc-compact")) {
	apply plugin: "java"

	// All projects use the base classpath
    java {
		// Only use SquirrelJME's boot classes, not the system SDK
		Project cldcCompactProject = project(":modules:cldc-compact")
		compileJava.options.bootstrapClasspath = project.objects.
			fileCollection().from(cldcCompactProject.buildDir.toPath().
			resolve("classes").resolve("java").resolve("main")).getAsFileTree()

		// Copy to JavaDoc
		javadoc.options.bootClasspath = compileJava.options.bootstrapClasspath
			.collect()
    }

	dependencies {
		// All projects depend on the Compact CLDC Library
		implementation project(":modules:cldc-compact")
		
		testImplementation project(":modules:cldc-compact")
	}
}
