import cc.squirreljme.plugin.tasks.MimeDecodeResourcesTask
import lombok.Getter

import javax.inject.Inject
import java.nio.file.Files

plugins {
	id "java"
	id "application"
}

apply plugin: "com.github.johnrengelman.shadow"

description = "SquirrelJME Font Compiler."
mainClassName = "cc.squirreljme.fontcompile.Main"

tasks.register("mimeDecode", MimeDecodeResourcesTask.class,
	"main",
	tasks.named("processResources").get(),
	tasks.named("clean").get())

processResources.dependsOn("mimeDecode")
jar.dependsOn("mimeDecode")

dependencies {
	implementation project(":emulators:emulator-base")
	implementation project(":modules:cldc-compact")
	implementation project(":modules:io")
	
	// Lombok for simpler getters/setters
	implementation 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

class CompileFontTask extends DefaultTask {
	/** The font path. */
	@Getter
	java.nio.file.Path fontPath
	
	@Inject
	public CompileFontTask(String __pathy) {
		if (__pathy == null) {
			throw new NullPointerException("No path.")
		}
		
		this.group = "squirreljme"
		this.description = "Compiles font ${__pathy}."
		
		// We need the actual program first
		this.dependsOn(this.project.provider({ ->
			this.project.tasks.getByName("shadowJar")}))
		
		// Determine where the font is
		java.nio.file.Path fontPath = this.project.rootProject.rootDir
			.toPath().resolve("assets").resolve("font").resolve(__pathy)
		this.fontPath = fontPath
		
		// Set as input
		if (Files.isDirectory(fontPath)) {
			this.inputs.dir(fontPath)
		} else {
			this.inputs.file(fontPath)
		}
		
		this.doFirst({Task tasky ->
			
		})
	}
}

// Generic task to compile all fonts
TaskProvider<Task> compileFonts = tasks.register("compileFonts") {
	this.group = "squirreljme"
	this.description = "Compiles all fonts."
}

for (String fn in [
	"monospace.sfdir/8.strike",
	"monospace.sfdir/16.strike",
	"monospace.sfdir/24.strike",
	"sansserif.sfdir/8.strike",
	"sansserif.sfdir/16.strike",
	"sansserif.sfdir/24.strike",
	"serif.sfdir/8.strike",
	"serif.sfdir/16.strike",
	"serif.sfdir/24.strike",
	"misaki8.bdf",
	"misaki12.bdf",
	"unifont16.bdf",
	"unifont-upper16.bdf"]) {
	// Determine a better name for it
	String shortish = fn.substring(0, 1).toUpperCase() + fn.replaceAll(
		"(\\.sfdir|\\.strike|\\.bdf|/)", "").substring(1)
	
	// Register it
	TaskProvider<Task> compileFont = tasks.register("compileFont${shortish}",
		CompileFontTask.class,
		fn)
	
	// Include in compilation of all fonts
	compileFonts.get().dependsOn(compileFont)
}
